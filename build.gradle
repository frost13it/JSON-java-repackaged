plugins {
    id 'java'
    id 'maven-publish'
    id 'com.jfrog.bintray' version '1.8.4'
}

group = "tk.frostbit"
archivesBaseName = "json"
version = "20180813"

def srcPackage = "org.json"
def dstPackage = "tk.frostbit.org.json"

def sourcesDir = new File(buildDir, "processedSources")

sourceCompatibility = "1.6"

sourceSets {
    main {
        java.srcDirs = [sourcesDir]
    }
}

task processSources(type: Sync) {
    destinationDir = sourcesDir

    from(projectDir) {
        into dstPackage.replace('.', '/')

        include 'JSONArray.java'
        include 'JSONException.java'
        include 'JSONObject.java'
        include 'JSONPointer.java'
        include 'JSONPointerException.java'
        include 'JSONPropertyIgnore.java'
        include 'JSONPropertyName.java'
        include 'JSONString.java'
        include 'JSONStringer.java'
        include 'JSONTokener.java'
        include 'JSONWriter.java'

        filter { line ->
            line.replace("package $srcPackage", "package $dstPackage")
                    .replace("import $srcPackage", "import $dstPackage")
        }
    }
}

compileJava.dependsOn processSources

jar {
    manifest.attributes "Automatic-Module-Name": "tk.frostbit.org.json"
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = "sources"
    from sourceSets.main.allSource
}

assemble.dependsOn sourcesJar

javadoc {
    failOnError = false
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = "javadoc"
    from javadoc.destinationDir
}

publishing {
    publications {
        maven(MavenPublication) {
            artifactId = archivesBaseName

            from components.java
            artifact sourcesJar
            artifact javadocJar

            pom {
                name = "JSON in Java (repackaged)"
                url = "https://github.com/frost13it/JSON-java-repackaged"
                licenses {
                    license {
                        name = "The JSON License"
                        url = "http://json.org/license.html"
                    }
                }
                scm {
                    url = "https://github.com/frost13it/JSON-java-repackaged"
                }
            }
        }
    }
}

task install(dependsOn: publishToMavenLocal)

assemble.dependsOn javadocJar

bintray {
    user = bintrayUser
    key = bintrayKey
    publications = ["maven"]

    pkg {
        repo = "3rd-party"
        name = "org.json"

        version {
            name = project.version
            vcsTag = name
        }
    }
}
